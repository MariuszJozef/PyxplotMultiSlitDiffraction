filename = "Plots/InterferenceMultiSlit.pdf"
set terminal pdf
set output "%s"%filename
set width 13
set numeric sigfig 3

PhaseWithoutSlitNumber(x, slitSeparationMm, wavelengthMm, screenPerpDistanceMm) = \
    pi * slitSeparationMm * x / wavelengthMm / sqrt(screenPerpDistanceMm**2 + x**2)

InterferenceAmplitude(x, slitNumber, slitSeparationMm, wavelengthMm, screenPerpDistanceMm) = \
    (slitNumber == 0) ? 0 : \
    sin(slitNumber * PhaseWithoutSlitNumber(x, slitSeparationMm, wavelengthMm, screenPerpDistanceMm)) \
    / slitNumber / sin(PhaseWithoutSlitNumber(x, slitSeparationMm, wavelengthMm, screenPerpDistanceMm))

InterferenceIntensityMultiSlit(x, slitNumber, slitSeparationMm, wavelengthMm, screenPerpDistanceMm) = \
    InterferenceAmplitude(x, slitNumber, slitSeparationMm, wavelengthMm, screenPerpDistanceMm)**2


set title "Interference of Light due to Multiple Slits"
set xlabel "Distance $x$ (mm) along viewing screen subtended by angle $\Theta$"
set ylabel "Relative intensity of light $I_2(\Theta) / I_2(0)$" rotate 0
set key below

slitNumber = 5
slitWidthMm = 0.05
slitSeparationMm = 2 * slitWidthMm
wavelengthMm = 5.1E-4
screenPerpDistanceMm = 500
maxAngleToObservationPointDeg = 1.5
xMax = screenPerpDistanceMm * tan(pi / 180 * maxAngleToObservationPointDeg)
xMin = -xMax
xTickStep = (xMax - xMin) / 5
mxTickStep = xTickStep / 2
set xrange [xMin : xMax]
set xtics xMin, xTickStep, xMax
set mxtics xMin, mxTickStep, xMax

yMin = -0.1
yMax = 1.1
yTickStep = (yMax - yMin) / 3
myTickStep = yTickStep / 4
yExtra = 0
set yrange [yMin - yExtra : yMax + yExtra]
set ytics yMin, yTickStep, yMax
set mytics yMin, myTickStep, yMax

set grid x1y1x2y2
set gridmajcolor grey
set gridmincolor grey

latexExpression1 = "$N = %d$ (slit number), " %(slitNumber)
latexExpression1 += "$d = %.1f$ mm (slit separation)" %(slitSeparationMm)

latexExpression2 = "$I_2(\Theta) / I_2(0) = \left( \
                   \frac{ \
                       \sin \Bigl( N \pi d x / \bigl( \lambda \sqrt{D^2 + x^2} \bigr) \Bigr) \
                   }{ \
                       N \sin \Bigl( \pi d x / \bigl( \lambda \sqrt{D^2 + x^2} \bigr) \Bigr) \
                   } \right)^2$"

latexExpression3 = "$\lambda = %.5f$ mm (wavelength of light), " %(wavelengthMm)
latexExpression3 += "$D = %d$ mm (distance to screen)" %(screenPerpDistanceMm)


plotPointsPerUnitLength = 30
plotPoints = plotPointsPerUnitLength * (xMax - xMin)
xIncrement = (xMax - xMin) / (plotPoints - 1)
print "plotPointsPerUnitLength = %d, plotPoints = %d, xIncrement = %.2E" %(plotPointsPerUnitLength, plotPoints, xIncrement)

set samples plotPoints

plot InterferenceIntensityMultiSlit(x, slitNumber, slitSeparationMm, wavelengthMm, screenPerpDistanceMm) \
    with linetype 1 linewidth 2 color 2 title \
    r"\parbox[t]{13cm}{%s \\[1ex] %s \\[1ex] %s}" \
    %(latexExpression1, latexExpression2, latexExpression3)

print "Output written to file: " + filename

filename = "Plots/InterferenceMultiSlit.jpeg"
set terminal jpeg
set output "%s" %filename
refresh
print "Output written to file: " + filename
